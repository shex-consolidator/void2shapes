prefix : <http://example.org/>
prefix rdf: 	 <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

CONSTRUCT {
 ?x :p ?firstItem .
 ?firstItem   rdf:first ?yFirst .
 ?currentItem rdf:first ?currentY .
 ?currentItem rdf:rest ?nextItem .
 ?lastItem    rdf:rest rdf:nil .
 ?lastItem    rdf:first ?yLast . 
}
where {
    ?x :p ?yFirst .
    # Bind the first and last values
    {
        SELECT ?x (Min(?y) as ?yFirst) (Max(?y) as ?yLast) 
        WHERE {
            ?x :p ?y .
        } GROUP BY ?x 
    } 
    
    # Bind the first and next values except the last
    {
        Select ?currentY (Min(?z) as ?yNext)
        where {
            ?x :p ?currentY .
            ?x :p ?z .
            Filter (STR(?z) > STR(?currentY))
        } group by ?x ?currentY
    }

    BIND(IRI(CONCAT(STR(?x), '-li-', STR(?yFirst))) AS ?firstItem)
    BIND(IRI(CONCAT(STR(?x), '-li-', STR(?currentY))) AS ?currentItem)
    BIND(IRI(CONCAT(STR(?x), '-li-', STR(?yNext))) AS ?nextItem)
	BIND(IRI(CONCAT(STR(?x), '-li-', STR(?yLast))) AS ?lastItem)
}
